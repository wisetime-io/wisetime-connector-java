/*
 * Copyright (c) 2018 Practice Insight Pty Ltd. All Rights Reserved.
 */

buildscript {
    repositories {
        maven {
            url 'https://storage.googleapis.com/wise-repo/repos/plugins/'
        }
        maven {
            // WT published releases
            url 'https://s3.eu-central-1.amazonaws.com/artifacts.wisetime.com/mvn2/plugins'
            content {
                includeGroup "io.wisetime"
            }
        }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:3.3.4"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.20.0"
        classpath "io.wisetime:validate-version:0.2.4"
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'application'
    id 'com.google.cloud.tools.jib' version '0.10.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id "com.github.ben-manes.versions" version '0.21.0'
    id "fr.brouillard.oss.gradle.jgitver" version "0.8.0"

}

repositories {
    maven {
        // Google Maven Central mirror
        url 'https://maven-central.storage-download.googleapis.com/repos/central/data'
    }
}

apply plugin: 'org.openapi.generator'
apply from: "$rootDir/gradle/conf/checkstyle.gradle"
apply from: "$rootDir/gradle/conf/jacoco.gradle"
group = 'io.wisetime'

archivesBaseName = 'wisetime-connector'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'io.wisetime.connector.example.FolderWatchConnectApp'

jgitver {
    autoIncrementPatch false
}

openApiGenerate {
    generatorName = "jaxrs-spec"
    inputSpec = "https://raw.githubusercontent.com/wisetime-io/connect-api-spec/master/spec/openapi.yaml".toString()
    outputDir = "$projectDir".toString()
    modelPackage = "io.wisetime.generated.connect"
}

task buildModel(dependsOn: tasks.openApiGenerate) {
    // creates latest jackson object model
    // after creating latest auto-generated object model, delete irrelevant files
    doLast {
        delete "${projectDir}/.openapi-generator"
        delete "${projectDir}/pom.xml"
        delete "${projectDir}/src/gen/java/org"
        delete "${projectDir}/src/main/openapi"
        delete "${projectDir}/src/gen/java/io/wisetime/generated/RestApplication.java"
    }
}

compileJava.dependsOn buildModel
tasks.jib.dependsOn compileJava

sourceSets {
    main {
        java {
            // include auto-generated model files from spec in additional to our main bundle
            srcDir 'src/gen/java'
            srcDir 'src/main/java'
        }
    }
}

def taskRequestString = getGradle().getStartParameter().getTaskRequests().toString()
if (taskRequestString.contains("closeAndReleaseRepository")) {
    apply from: "$rootDir/gradle/publish_maven_central.gradle"
} else if (taskRequestString.contains("publish")) {
    apply from: "$rootDir/gradle/publish_s3_repo.gradle"
}

if (taskRequestString.contains("dependencyUpdates") || taskRequestString.contains("versionCheck")) {
    // add exclusions for reporting on updates and vulnerabilities
    apply from: "$rootDir/gradle/versionPluginConfig.gradle"
}

dependencies {
    compile('io.wisetime:wise-log-aws:3.1.0') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.dataformat', module: 'jackson-dataformat-cbor'
    }

    // spark-core 2.8.0 includes jetty runtime 9.4.12.v20180830
    compile 'com.sparkjava:spark-core:2.9.0'

    compile('com.sparkjava:spark-template-thymeleaf:2.7.1') {
        exclude group: 'com.sparkjava', module: 'spark-core'
        exclude group: 'org.thymeleaf', module: 'thymeleaf'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    compile 'org.thymeleaf:thymeleaf:3.0.5.RELEASE'

    compile 'org.apache.commons:commons-configuration2:2.4'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'net.jodah:failsafe:1.1.0'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.7'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.7'

    compile 'info.picocli:picocli:3.7.0'
    compile 'org.apache.commons:commons-collections4:4.2'
    compile 'com.google.guava:guava:27.1-jre'
    compile 'commons-io:commons-io:2.6'

    compile 'org.xerial:sqlite-jdbc:3.27.2.1'
    compile 'org.codejargon:fluentjdbc:1.8.3'

    compile 'org.apache.httpcomponents:httpcore:4.4.10'
    compile('org.apache.httpcomponents:fluent-hc:4.5.6') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    compile 'commons-codec:commons-codec:1.12'

    compile 'ch.qos.logback:logback-core:1.2.3'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.26'

    // required by activity text template engine
    compile 'org.freemarker:freemarker:2.3.28'

    // swagger-annotations and validation-api used in openApiGenerate
    compile 'io.swagger:swagger-annotations:1.5.3'
    compile 'javax.validation:validation-api:2.0.1.Final'

    testCompile('com.github.javafaker:javafaker:0.17.2') {
        exclude group: 'org.apache.commons', module: 'commons-lang3'
    }

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    testCompile 'org.mockito:mockito-core:2.22.0'
    testCompile 'org.objenesis:objenesis:2.6'
    testCompile 'org.assertj:assertj-core:3.12.2'
    testCompile('io.github.benas:random-beans:3.9.0') {
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'org.objenesis', module: 'objenesis'
    }
}

test {
    useJUnitPlatform()
    testLogging {
        // "passed", "skipped", "failed"
        events 'skipped', 'failed'
    }
}

ext.jacocoLimits = [
        'instruction': 72,
        'line'       : 72,
        'method'     : 74,
        'class'      : 90
]

tasks.withType(Checkstyle) {
    source = sourceSets.main.allJava.matching {
        exclude '**/generated/**/*'
    }
}

clean {
    delete "${projectDir}/out"
    delete "${projectDir}/.openapi-generator"
    delete "${projectDir}/pom.xml"
    delete "${projectDir}/src/gen/java"
}
