apply plugin: "jacoco"

ext {
    jcLineMin = 0.5
    jcClassMin = 0.8
    jcInstructionMin = 0.4

    jacocoExcludes = [
        'io/wisetime/connector/time_poster/**',
        // code coverage candidates:
        'io/wisetime/connector/controller/**',
        'io/wisetime/generated/connect/**'
    ]
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
    sourceDirectories.from = files(sourceDirectories.files.collect {
        fileTree(dir: it, exclude: jacocoExcludes)
    })
    classDirectories.from = files(classDirectories.files.collect {
        fileTree(dir: it, exclude: jacocoExcludes)
    })
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            // https://docs.gradle.org/4.10/javadoc/org/gradle/testing/jacoco/tasks/rules/JacocoViolationRule.html
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = jcLineMin
            }
        }
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = jcClassMin
            }
        }
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = jcInstructionMin
            }
        }
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
